cmake_minimum_required(VERSION 3.8)
project(NatsuLang CXX)

set(CMAKE_MODULE_PATH ${CMAKE_MODULE_PATH} "${CMAKE_SOURCE_DIR}/CMake/Modules")

set(${PROJECT_NAME}_EXTERN_DIR "${${PROJECT_NAME}_SOURCE_DIR}/Extern")

option(BUILD_ASTINTERPRETER "Build ASTInterpreter" ON)
option(BUILD_AOTCOMPILER "Build AotCompiler" OFF)
option(BUILD_SHARED_LIBS "Build shared libraries" OFF)

add_subdirectory("${${PROJECT_NAME}_EXTERN_DIR}/NatsuLib")
set(NatsuLib_INCLUDE_DIRS ${NatsuLib_SOURCE_DIR}
	CACHE INTERNAL "NatsuLib: Include Directories" FORCE)

if(CMAKE_CXX_COMPILER_ID MATCHES "MSVC")
	if(BUILD_SHARED_LIBS)
		message(FATAL_ERROR "Cannot build shared libraries on msvc.")
	endif()
	if(MSVC_VERSION GREATER_EQUAL "1900")
		include(CheckCXXCompilerFlag)
		CHECK_CXX_COMPILER_FLAG("/std:c++latest" _cpp_latest_flag_supported)
		if(_cpp_latest_flag_supported)
			add_compile_options("/std:c++latest")
		else()
			set(CMAKE_CXX_STANDARD 17)
		endif()
	else()
		set(CMAKE_CXX_STANDARD 17)
	endif()
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /W3 /WX /wd4100 /wd4996")
elseif(CMAKE_CXX_COMPILER_ID MATCHES "GNU" OR CMAKE_CXX_COMPILER_ID MATCHES "Clang")
	set(CMAKE_CXX_STANDARD 17)
	set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -Wall -Wextra -Wno-unused -Werror")
	if(BUILD_SHARED_LIBS)
		message(WARNING "Building shared libraries has not been tested, use at your own risk.")
		set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} -fPIC")
	endif()
endif()

add_subdirectory(NatsuLang)
set(NatsuLang_INCLUDE_DIRS "${${PROJECT_NAME}_SOURCE_DIR}/${PROJECT_NAME}/include"
	CACHE INTERNAL "NatsuLang: Include Directories" FORCE)

if(BUILD_ASTINTERPRETER)
	add_subdirectory("NatsuLang.ASTInterpreter")
	set(NatsuLang_ASTInterpreter_INCLUDE_DIRS "${${PROJECT_NAME}_SOURCE_DIR}/NatsuLang.ASTInterpreter")
	add_subdirectory("NatsuLang.ASTInterpreter.Cli")
endif()

if(BUILD_AOTCOMPILER)
	find_package(LLVM)
	if(NOT LLVM_FOUND)
		message(FATAL_ERROR "LLVM not found, aborting.")
	endif()
	add_subdirectory("NatsuLang.AOTCompiler")
	set(NatsuLang_AOTCompiler_INCLUDE_DIRS "${${PROJECT_NAME}_SOURCE_DIR}/NatsuLang.AOTCompiler")
	add_subdirectory("NatsuLang.AOTCompiler.Cli")
endif()
